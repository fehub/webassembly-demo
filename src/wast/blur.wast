(module
    (memory 1024 1024
        (segment 0 "\00\02\00\00\00\02\00\00\c8\01\00\00\00\02\00\00\48\01\00\00\c8\01\00\00\4f\01\00\00\00\02\00\00\95\01\00\00\48\01\00\00\0f\01\00\00\c8\01\00\00\84\01\00\00\4f\01\00\00\24\01\00\00\00\02\00\00\c6\01\00\00\95\01\00\00\6c\01\00\00\48\01\00\00\2a\01\00\00\0f\01\00\00\f0\01\00\00\c8\01\00\00\a4\01\00\00\84\01\00\00\68\01\00\00\4f\01\00\00\38\01\00\00\24\01\00\00\11\01\00\00\00\02\00\00\e2\01\00\00\c6\01\00\00\ac\01\00\00\95\01\00\00\7f\01\00\00\6c\01\00\00\59\01\00\00\48\01\00\00\38\01\00\00\2a\01\00\00\1c\01\00\00\0f\01\00\00\03\01\00\00\f0\01\00\00\db\01\00\00\c8\01\00\00\b5\01\00\00\a4\01\00\00\94\01\00\00\84\01\00\00\76\01\00\00\68\01\00\00\5b\01\00\00\4f\01\00\00\43\01\00\00\38\01\00\00\2e\01\00\00\24\01\00\00\1a\01\00\00\11\01\00\00\09\01\00\00\00\02\00\00\f1\01\00\00\e2\01\00\00\d4\01\00\00\c6\01\00\00\b9\01\00\00\ac\01\00\00\a1\01\00\00\95\01\00\00\8a\01\00\00\7f\01\00\00\75\01\00\00\6c\01\00\00\62\01\00\00\59\01\00\00\51\01\00\00\48\01\00\00\40\01\00\00\38\01\00\00\31\01\00\00\2a\01\00\00\23\01\00\00\1c\01\00\00\16\01\00\00\0f\01\00\00\09\01\00\00\03\01\00\00\fb\01\00\00\f0\01\00\00\e5\01\00\00\db\01\00\00\d1\01\00\00\c8\01\00\00\be\01\00\00\b5\01\00\00\ac\01\00\00\a4\01\00\00\9c\01\00\00\94\01\00\00\8c\01\00\00\84\01\00\00\7d\01\00\00\76\01\00\00\6f\01\00\00\68\01\00\00\62\01\00\00\5b\01\00\00\55\01\00\00\4f\01\00\00\49\01\00\00\43\01\00\00\3e\01\00\00\38\01\00\00\33\01\00\00\2e\01\00\00\29\01\00\00\24\01\00\00\1f\01\00\00\1a\01\00\00\16\01\00\00\11\01\00\00\0d\01\00\00\09\01\00\00\05\01\00\00\00\02\00\00\f9\01\00\00\f1\01\00\00\e9\01\00\00\e2\01\00\00\db\01\00\00\d4\01\00\00\cd\01\00\00\c6\01\00\00\bf\01\00\00\b9\01\00\00\b3\01\00\00\ac\01\00\00\a6\01\00\00\a1\01\00\00\9b\01\00\00\95\01\00\00\8f\01\00\00\8a\01\00\00\85\01\00\00\7f\01\00\00\7a\01\00\00\75\01\00\00\70\01\00\00\6c\01\00\00\67\01\00\00\62\01\00\00\5e\01\00\00\59\01\00\00\55\01\00\00\51\01\00\00\4c\01\00\00\48\01\00\00\44\01\00\00\40\01\00\00\3c\01\00\00\38\01\00\00\35\01\00\00\31\01\00\00\2d\01\00\00\2a\01\00\00\26\01\00\00\23\01\00\00\1f\01\00\00\1c\01\00\00\19\01\00\00\16\01\00\00\12\01\00\00\0f\01\00\00\0c\01\00\00\09\01\00\00\06\01\00\00\03\01\00\00\01\01\00\00\fb\01\00\00\f5\01\00\00\f0\01\00\00\eb\01\00\00\e5\01\00\00\e0\01\00\00\db\01\00\00\d6\01\00\00\d1\01\00\00\cc\01\00\00\c8\01\00\00\c3\01\00\00\be\01\00\00\ba\01\00\00\b5\01\00\00\b1\01\00\00\ac\01\00\00\a8\01\00\00\a4\01\00\00\a0\01\00\00\9c\01\00\00\98\01\00\00\94\01\00\00\90\01\00\00\8c\01\00\00\88\01\00\00\84\01\00\00\81\01\00\00\7d\01\00\00\79\01\00\00\76\01\00\00\72\01\00\00\6f\01\00\00\6b\01\00\00\68\01\00\00\65\01\00\00\62\01\00\00\5e\01\00\00\5b\01\00\00\58\01\00\00\55\01\00\00\52\01\00\00\4f\01\00\00\4c\01\00\00\49\01\00\00\46\01\00\00\43\01\00\00\40\01\00\00\3e\01\00\00\3b\01\00\00\38\01\00\00\36\01\00\00\33\01\00\00\30\01\00\00\2e\01\00\00\2b\01\00\00\29\01\00\00\26\01\00\00\24\01\00\00\21\01\00\00\1f\01\00\00\1d\01\00\00\1a\01\00\00\18\01\00\00\16\01\00\00\13\01\00\00\11\01\00\00\0f\01\00\00\0d\01\00\00\0b\01\00\00\09\01\00\00\07\01\00\00\05\01\00\00\03\01\00\00")
        (segment 1024 "\09\0b\0c\0d\0d\0e\0e\0f\0f\0f\0f\10\10\10\10\11\11\11\11\11\11\11\12\12\12\12\12\12\12\12\12\13\13\13\13\13\13\13\13\13\13\13\13\13\13\14\14\14\14\14\14\14\14\14\14\14\14\14\14\14\14\14\14\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\15\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\16\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\17\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18\18")    
    )
    (export "memory" memory)

    (func $createStack (param i32) (result i32)
        (i64.store (get_local 0) (i64.const 0))
        (get_local 0)
    )

    (func $nextStack (param i32) (result i32)
        (i32.load offset=4 (get_local 0))
    )

    (func $setRed (param $offset i32) (param $x i32) (result i32)
        (i32.store8 (get_local $offset) (get_local $x))
        (get_local $x)
    )
    (func $setGreen (param $offset i32) (param $x i32) (result i32)
        (i32.store8 offset=1 (get_local $offset) (get_local $x))
        (get_local $x)
    )
    (func $setBlue (param $offset i32) (param $x i32) (result i32)
        (i32.store8 offset=2 (get_local $offset) (get_local $x))
        (get_local $x)
    )

    (func $setRedToImagePixel (param $offset i32) (param $x i32) (result i32)
        (i32.store8 offset=8192 (get_local $offset) (get_local $x))
        (get_local $x)
    )
    (func $setGreenToImagePixel (param $offset i32) (param $x i32) (result i32)
        (i32.store8 offset=8193 (get_local $offset) (get_local $x))
        (get_local $x)
    )
    (func $setBlueToImagePixel (param $offset i32) (param $x i32) (result i32)
        (i32.store8 offset=8194 (get_local $offset) (get_local $x))
        (get_local $x)
    )

    (func $getRed   (param i32) (result i32) (i32.load8_u (get_local 0)))
    (func $getGreen (param i32) (result i32) (i32.load8_u offset=1 (get_local 0)))
    (func $getBlue  (param i32) (result i32) (i32.load8_u offset=2 (get_local 0)))

    (func $getRedFromImagePixel   (param i32) (result i32) (i32.load8_u offset=8192 (get_local 0)))
    (func $getGreenFromImagePixel (param i32) (result i32) (i32.load8_u offset=8193 (get_local 0)))
    (func $getBlueFromImagePixel  (param i32) (result i32) (i32.load8_u offset=8194 (get_local 0)))

    (func $mul_table (param i32) (result i32)
        (i32.load (i32.shl (get_local 0) (i32.const 2)))
    )
    (export "mul_table" $mul_table)

    (func $shg_table (param i32) (result i32)
        (i32.load8_u offset=1024 (get_local 0))
    )
    (export "shg_table" $shg_table)

    (func $blur (param $width i32) (param $height i32) (param $radius i32)
        (local $x i32)
        (local $y i32)
        (local $i i32)
        (local $p i32)
        (local $yp i32)
        (local $yi i32)
        (local $yw i32)
        (local $r_sum i32)
        (local $g_sum i32)
        (local $b_sum i32)
        (local $a_sum i32)
        (local $r_out_sum i32)
        (local $g_out_sum i32)
        (local $b_out_sum i32)
        (local $a_out_sum i32)
        (local $r_in_sum i32)
        (local $g_in_sum i32)
        (local $b_in_sum i32)
        (local $a_in_sum i32)
        (local $pr i32)
        (local $pg i32)
        (local $pb i32)
        (local $pa i32)
        (local $rbs i32)
        (local $div i32)
        (local $widthMinus1 i32)
        (local $heightMinus1 i32)
        (local $radiusPlus1 i32)
        (local $sumFactor i32)
        (local $stack i32)
        (local $stackStart i32)
        (local $stackEnd i32)
        (local $stackIn i32)
        (local $stackOut i32)
        (local $mul_sum i32)
        (local $shg_sum i32)
        
        (set_local $div (i32.add (i32.add (get_local $radius) (get_local $radius)) (i32.const 1)))
        (set_local $widthMinus1 (i32.sub (get_local $width) (i32.const 1)))
        (set_local $heightMinus1 (i32.sub (get_local $height) (i32.const 1)))
        (set_local $radiusPlus1 (i32.add (get_local $radius) (i32.const 1)))
        (set_local $sumFactor
            (i32.mul
                (get_local $radiusPlus1)
                (i32.div_u
                    (i32.add (get_local $radiusPlus1) (i32.const 1))
                    (i32.const 2)
                )
            )
        )

        (set_local $stackStart (call $createStack (i32.const 1280)))
        (set_local $stack (get_local $stackStart))
        (set_local $i (i32.const 1))
        (loop $exit $cont
            (br_if $exit (i32.ge_u (get_local $i) (get_local $div)))
            (i32.store offset=4 (get_local $stack) (call $createStack (i32.add (get_local $stack) (i32.const 8))))
            (set_local $stack (call $nextStack (get_local $stack)))
            (if (i32.eq (get_local $i) (get_local $radiusPlus1))
                (set_local $stackEnd (get_local $stack))
            )
            (set_local $i (i32.add (get_local $i) (i32.const 1)))
            (br $cont)
        )
        (i32.store offset=4 (get_local $stack) (get_local $stackStart))

        (set_local $yw (i32.const 0))
        (set_local $yi (i32.const 0))

        (set_local $mul_sum (call $mul_table (get_local $radius)))
        (set_local $shg_sum (call $shg_table (get_local $radius)))

        (set_local $y (i32.const 0))
        (loop $exit $cont
            ;; (br_if $exit (i32.ge_u (get_local $y) (i32.const 2)))
            (br_if $exit (i32.ge_u (get_local $y) (get_local $height)))

            (set_local $r_in_sum (i32.const 0))
            (set_local $g_in_sum (i32.const 0))
            (set_local $b_in_sum (i32.const 0))
            (set_local $r_sum (i32.const 0))
            (set_local $g_sum (i32.const 0))
            (set_local $b_sum (i32.const 0))

            (set_local $r_out_sum (i32.mul (get_local $radiusPlus1) (set_local $pr (call $getRedFromImagePixel (get_local $yi)))))
            (set_local $g_out_sum (i32.mul (get_local $radiusPlus1) (set_local $pg (call $getGreenFromImagePixel (get_local $yi)))))
            (set_local $b_out_sum (i32.mul (get_local $radiusPlus1) (set_local $pb (call $getBlueFromImagePixel (get_local $yi)))))

            (set_local $r_sum (i32.add (get_local $r_sum) (i32.mul (get_local $sumFactor) (get_local $pr))))
            (set_local $g_sum (i32.add (get_local $g_sum) (i32.mul (get_local $sumFactor) (get_local $pg))))
            (set_local $b_sum (i32.add (get_local $b_sum) (i32.mul (get_local $sumFactor) (get_local $pb))))
            
            (set_local $stack (get_local $stackStart))

            (set_local $i (i32.const 0))
            (loop $exit $cont
                (br_if $exit (i32.ge_u (get_local $i) (get_local $radiusPlus1)))
                (call $setRed (get_local $stack) (get_local $pr))
                (call $setGreen (get_local $stack) (get_local $pg))
                (call $setBlue (get_local $stack) (get_local $pb))
                (set_local $stack (call $nextStack (get_local $stack)))
                (set_local $i (i32.add (get_local $i) (i32.const 1)))
                (br $cont)
            )

            (set_local $i (i32.const 1))
            (loop $exit $cont
                (br_if $exit (i32.ge_u (get_local $i) (get_local $radiusPlus1)))
                (set_local $p
                    (i32.add
                        (get_local $yi)
                        (i32.shl
                            (if (i32.lt_u (get_local $widthMinus1) (get_local $i)) (get_local $widthMinus1) (get_local $i))                         
                            (i32.const 2)
                        )
                    )
                )
                (set_local $r_sum
                    (i32.add
                        (get_local $r_sum)
                        (i32.mul
                            (call $setRed (get_local $stack) (set_local $pr (call $getRedFromImagePixel (get_local $p))))
                            (set_local $rbs (i32.sub (get_local $radiusPlus1) (get_local $i)))
                        )
                    )
                )
                (set_local $g_sum
                    (i32.add
                        (get_local $g_sum)
                        (i32.mul
                            (call $setGreen (get_local $stack) (set_local $pg (call $getGreenFromImagePixel (get_local $p))))
                            (get_local $rbs)
                        )
                    )
                )
                (set_local $b_sum
                    (i32.add
                        (get_local $b_sum)
                        (i32.mul
                            (call $setBlue (get_local $stack) (set_local $pb (call $getBlueFromImagePixel (get_local $p))))
                            (get_local $rbs)
                        )
                    )
                )

                (set_local $r_in_sum (i32.add (get_local $r_in_sum) (get_local $pr)))
                (set_local $g_in_sum (i32.add (get_local $g_in_sum) (get_local $pg)))
                (set_local $b_in_sum (i32.add (get_local $b_in_sum) (get_local $pb)))

                (set_local $stack (call $nextStack (get_local $stack)))

                (set_local $i (i32.add (get_local $i) (i32.const 1)))
                (br $cont)
            )

            (set_local $stackIn (get_local $stackStart))
            (set_local $stackOut (get_local $stackEnd))
            (set_local $x (i32.const 0))
            (loop $exit $cont
                (br_if $exit (i32.ge_u (get_local $x) (get_local $width)))
                ;; (br_if $exit (i32.ge_u (get_local $x) (i32.const 1912)))
                
                (call $setRedToImagePixel (get_local $yi) (i32.shr_u (i32.mul (get_local $r_sum) (get_local $mul_sum)) (get_local $shg_sum)))
                (call $setGreenToImagePixel (get_local $yi) (i32.shr_u (i32.mul (get_local $g_sum) (get_local $mul_sum)) (get_local $shg_sum)))
                (call $setBlueToImagePixel (get_local $yi) (i32.shr_u (i32.mul (get_local $b_sum) (get_local $mul_sum)) (get_local $shg_sum)))
                
                (set_local $r_sum (i32.sub (get_local $r_sum) (get_local $r_out_sum)))
                (set_local $g_sum (i32.sub (get_local $g_sum) (get_local $g_out_sum)))
                (set_local $b_sum (i32.sub (get_local $b_sum) (get_local $b_out_sum)))

                (set_local $r_out_sum (i32.sub (get_local $r_out_sum) (call $getRed (get_local $stackIn))))
                (set_local $g_out_sum (i32.sub (get_local $g_out_sum) (call $getGreen (get_local $stackIn))))
                (set_local $b_out_sum (i32.sub (get_local $b_out_sum) (call $getBlue (get_local $stackIn))))

                (set_local $p
                    (i32.shl
                        (i32.add
                            (get_local $yw)
                            (if
                                (i32.lt_u
                                    (set_local $p (i32.add (get_local $x) (get_local $radiusPlus1)))
                                    (get_local $widthMinus1)
                                )
                                (get_local $p)
                                (get_local $widthMinus1)
                            )
                        )
                        (i32.const 2)
                    )
                )

                (set_local $r_in_sum
                    (i32.add
                        (get_local $r_in_sum)
                        (call $setRed (get_local $stackIn) (call $getRedFromImagePixel (get_local $p)))
                    )
                )
                (set_local $g_in_sum
                    (i32.add
                        (get_local $g_in_sum)
                        (call $setGreen (get_local $stackIn) (call $getGreenFromImagePixel (get_local $p)))
                    )
                )
                (set_local $b_in_sum
                    (i32.add
                        (get_local $b_in_sum)
                        (call $setBlue (get_local $stackIn) (call $getBlueFromImagePixel (get_local $p)))
                    )
                )

                (set_local $r_sum (i32.add (get_local $r_sum) (get_local $r_in_sum)))
                (set_local $g_sum (i32.add (get_local $g_sum) (get_local $g_in_sum)))
                (set_local $b_sum (i32.add (get_local $b_sum) (get_local $b_in_sum)))

                (set_local $stackIn (call $nextStack (get_local $stackIn)))

                (set_local $r_out_sum (i32.add (get_local $r_out_sum) (set_local $pr (call $getRed (get_local $stackOut)))))
                (set_local $g_out_sum (i32.add (get_local $g_out_sum) (set_local $pg (call $getGreen (get_local $stackOut)))))
                (set_local $b_out_sum (i32.add (get_local $b_out_sum) (set_local $pb (call $getBlue (get_local $stackOut)))))

                (set_local $r_in_sum (i32.sub (get_local $r_in_sum) (get_local $pr)))
                (set_local $g_in_sum (i32.sub (get_local $g_in_sum) (get_local $pg)))
                (set_local $b_in_sum (i32.sub (get_local $b_in_sum) (get_local $pb)))

                (set_local $stackOut (call $nextStack (get_local $stackOut)))

                (set_local $yi (i32.add (get_local $yi) (i32.const 4)))

                (set_local $x (i32.add (get_local $x) (i32.const 1)))
                (br $cont)
            )

            (set_local $yw (i32.add (get_local $yw) (get_local $width)))

            (set_local $y (i32.add (get_local $y) (i32.const 1)))
            (br $cont)
        )

        (set_local $x (i32.const 0))
        (loop $exit $cont
            (br_if $exit (i32.ge_u (get_local $x) (get_local $width)))

            (set_local $r_in_sum (i32.const 0))
            (set_local $g_in_sum (i32.const 0))
            (set_local $b_in_sum (i32.const 0))
            (set_local $r_sum (i32.const 0))
            (set_local $g_sum (i32.const 0))
            (set_local $b_sum (i32.const 0))
            
            (set_local $yi (i32.shl (get_local $x) (i32.const 2)))

            (set_local $r_out_sum (i32.mul (get_local $radiusPlus1) (set_local $pr (call $getRedFromImagePixel (get_local $yi)))))
            (set_local $g_out_sum (i32.mul (get_local $radiusPlus1) (set_local $pg (call $getGreenFromImagePixel (get_local $yi)))))
            (set_local $b_out_sum (i32.mul (get_local $radiusPlus1) (set_local $pb (call $getBlueFromImagePixel (get_local $yi)))))

            (set_local $r_sum (i32.add (get_local $r_sum) (i32.mul (get_local $sumFactor) (get_local $pr))))
            (set_local $g_sum (i32.add (get_local $g_sum) (i32.mul (get_local $sumFactor) (get_local $pg))))
            (set_local $b_sum (i32.add (get_local $b_sum) (i32.mul (get_local $sumFactor) (get_local $pb))))
            
            (set_local $stack (get_local $stackStart))

            (set_local $i (i32.const 0))
            (loop $exit $cont
                (br_if $exit (i32.ge_u (get_local $i) (get_local $radiusPlus1)))
                (call $setRed (get_local $stack) (get_local $pr))
                (call $setGreen (get_local $stack) (get_local $pg))
                (call $setBlue (get_local $stack) (get_local $pb))
                (set_local $stack (call $nextStack (get_local $stack)))
                (set_local $i (i32.add (get_local $i) (i32.const 1)))
                (br $cont)
            )

            (set_local $yp (get_local $width))

            (set_local $i (i32.const 1))
            (loop $exit $cont
                (br_if $exit (i32.ge_u (get_local $i) (get_local $radiusPlus1)))

                (set_local $yi (i32.shl (i32.add (get_local $yp) (get_local $x)) (i32.const 2)))

                (set_local $r_sum
                    (i32.add
                        (get_local $r_sum)
                        (i32.mul
                            (call $setRed (get_local $stack) (set_local $pr (call $getRedFromImagePixel (get_local $yi))))
                            (set_local $rbs (i32.sub (get_local $radiusPlus1) (get_local $i)))
                        )
                    )
                )
                (set_local $g_sum
                    (i32.add
                        (get_local $g_sum)
                        (i32.mul
                            (call $setGreen (get_local $stack) (set_local $pg (call $getGreenFromImagePixel (get_local $yi))))
                            (get_local $rbs)
                        )
                    )
                )
                (set_local $b_sum
                    (i32.add
                        (get_local $b_sum)
                        (i32.mul
                            (call $setBlue (get_local $stack) (set_local $pb (call $getBlueFromImagePixel (get_local $yi))))
                            (get_local $rbs)
                        )
                    )
                )

                (set_local $r_in_sum (i32.add (get_local $r_in_sum) (get_local $pr)))
                (set_local $g_in_sum (i32.add (get_local $g_in_sum) (get_local $pg)))
                (set_local $b_in_sum (i32.add (get_local $b_in_sum) (get_local $pb)))

                (set_local $stack (call $nextStack (get_local $stack)))

                (if (i32.lt_u (get_local $i) (get_local $heightMinus1))
                    (set_local $yp (i32.add (get_local $yp) (get_local $width)))
                )

                (set_local $i (i32.add (get_local $i) (i32.const 1)))
                (br $cont)
            )

            (set_local $yi (get_local $x))
            (set_local $stackIn (get_local $stackStart))
            (set_local $stackOut (get_local $stackEnd))
            (set_local $y (i32.const 0))
            (loop $exit $cont
                (br_if $exit (i32.ge_u (get_local $y) (get_local $height)))
                
                (set_local $p (i32.shl (get_local $yi) (i32.const 2)))
                (call $setRedToImagePixel (get_local $p) (i32.shr_u (i32.mul (get_local $r_sum) (get_local $mul_sum)) (get_local $shg_sum)))
                (call $setGreenToImagePixel (get_local $p) (i32.shr_u (i32.mul (get_local $g_sum) (get_local $mul_sum)) (get_local $shg_sum)))
                (call $setBlueToImagePixel (get_local $p) (i32.shr_u (i32.mul (get_local $b_sum) (get_local $mul_sum)) (get_local $shg_sum)))
                
                (set_local $r_sum (i32.sub (get_local $r_sum) (get_local $r_out_sum)))
                (set_local $g_sum (i32.sub (get_local $g_sum) (get_local $g_out_sum)))
                (set_local $b_sum (i32.sub (get_local $b_sum) (get_local $b_out_sum)))

                (set_local $r_out_sum (i32.sub (get_local $r_out_sum) (call $getRed (get_local $stackIn))))
                (set_local $g_out_sum (i32.sub (get_local $g_out_sum) (call $getGreen (get_local $stackIn))))
                (set_local $b_out_sum (i32.sub (get_local $b_out_sum) (call $getBlue (get_local $stackIn))))

                (set_local $p
                    (i32.shl
                        (i32.add
                            (get_local $x)
                            (i32.mul
                                (if
                                    (i32.lt_u
                                        (set_local $p (i32.add (get_local $y) (get_local $radiusPlus1)))
                                        (get_local $heightMinus1)
                                    )
                                    (get_local $p)
                                    (get_local $heightMinus1)
                                )
                                (get_local $width)
                            )
                        )
                        (i32.const 2)
                    )
                )

                (set_local $r_sum
                    (i32.add
                        (get_local $r_sum)
                        (set_local $r_in_sum
                            (i32.add
                                (get_local $r_in_sum)
                                (call $setRed (get_local $stackIn) (call $getRedFromImagePixel (get_local $p)))
                            )
                        )
                    )    
                )
                (set_local $g_sum
                    (i32.add
                        (get_local $g_sum)
                        (set_local $g_in_sum
                            (i32.add
                                (get_local $g_in_sum)
                                (call $setGreen (get_local $stackIn) (call $getGreenFromImagePixel (get_local $p)))
                            )
                        )
                    )    
                )
                (set_local $b_sum
                    (i32.add
                        (get_local $b_sum)
                        (set_local $b_in_sum
                            (i32.add
                                (get_local $b_in_sum)
                                (call $setBlue (get_local $stackIn) (call $getBlueFromImagePixel (get_local $p)))
                            )
                        )
                    )    
                )

                (set_local $stackIn (call $nextStack (get_local $stackIn)))

                (set_local $r_out_sum (i32.add (get_local $r_out_sum) (set_local $pr (call $getRed (get_local $stackOut)))))
                (set_local $g_out_sum (i32.add (get_local $g_out_sum) (set_local $pg (call $getGreen (get_local $stackOut)))))
                (set_local $b_out_sum (i32.add (get_local $b_out_sum) (set_local $pb (call $getBlue (get_local $stackOut)))))

                (set_local $r_in_sum (i32.sub (get_local $r_in_sum) (get_local $pr)))
                (set_local $g_in_sum (i32.sub (get_local $g_in_sum) (get_local $pg)))
                (set_local $b_in_sum (i32.sub (get_local $b_in_sum) (get_local $pb)))

                (set_local $stackOut (call $nextStack (get_local $stackOut)))

                (set_local $yi (i32.add (get_local $yi) (get_local $width)))

                (set_local $y (i32.add (get_local $y) (i32.const 1)))
                (br $cont)
            )

            (set_local $x (i32.add (get_local $x) (i32.const 1)))
            (br $cont)
        )
    )
    (export "blur" $blur)
)